@Library('juju-pipeline@master') _
pipeline {
    agent { label 'charm-runner' }
    /* XXX: Global $PATH setting doesn't translate properly in pipelines
     https://stackoverflow.com/questions/43987005/jenkins-does-not-recognize-command-sh
     */
    environment {
        PATH = "/snap/bin:/usr/sbin:/usr/bin:/sbin:/bin:/usr/local/bin"
        JUJU_REPOSITORY = "${env.WORKSPACE}/build/charms"
        TMPDIR = "${env.WORKSPACE}/tmp"
        JUJU_MODEL = "${params.model}-${params.charm}-${env.BUILD_NUMBER}"
        JUJU_CONTROLLER = "${params.controller}-${env.BUILD_NUMBER}"
    }
    options {
        ansiColor('xterm')
        timestamps()
    }
    triggers {
        pollSCM('H H/2 * * *')
    }
    stages {
        stage('Setup') {
            steps {
                sh "mkdir -p ${env.JUJU_REPOSITORY}"
                sh "mkdir -p ${env.TMPDIR}"
            }
        }
        stage('Build') {
            options {
                timeout(time: 15, unit: 'MINUTES')
            }
            steps {
                sh "git clone --depth 1 -q ${params.git_repo}"
                retry(5){
                    sh "cd ${params.repo_name} && charm build -r --no-local-layers --force"
                }
            }
        }
        stage('Test') {
            options {
                timeout(time: 2, unit: 'HOURS')
            }

            steps {
                dir("${JUJU_REPOSITORY}/builds/${params.charm}") {
                    sh "charm proof"
                    sh "flake8 --ignore E501,E722,E305,E241,E126,W291,W391,F811,E261,F841,F401"
                }
                dir("jobs") {
                    script {
                        def test_path = "integration/charm/test_${params.charm}.py"
                        if (fileExists(test_path)) {
                            sh "CHARM_PATH=${env.JUJU_REPOSITORY}/builds/${params.charm} CONTROLLER=${params.controller} MODEL=${env.JUJU_MODEL} tox -e py36 -- pytest --tb native -ra -v -s -n auto --junit-xml=${params.charm}.xml ${test_path}"
                        }
                    }

                }
            }
        }
        stage('Release to Store') {
            options {
                timeout(time: 5, unit: 'MINUTES')
            }
            steps {
                script {
                    def git_commit = sh script:"git rev-parse HEAD", returnStdout: true
                    // snap's need to pipe to cat so we can get the output
                    def charm = readYaml text:sh(script: "charm push ${env.JUJU_REPOSITORY}/builds/${params.charm} cs:~containers/${params.charm} | cat 2>&1", returnStdout: true)
                    sh(script: String.format("charm set '%s' commit='%s'",
                                             charm.url.trim(),
                                             git_commit.trim()))
                    sh(script: String.format("CHARM='%s' FROM_CHANNEL='%s' TO_CHANNEL='%s' jobs/build-charms/promote-charm.sh",
                                             charm.url.trim(),
                                             params.from_channel,
                                             params.to_channel))
                }
            }
        }
    }
}
